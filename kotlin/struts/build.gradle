buildscript {
    repositories {
        mavenCentral()
    }

    ext {
        kotlinVersion = '1.0.1-2'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'eclipse-wtp'
apply plugin: 'war'
apply plugin: 'kotlin'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

ext {
    projectName = 'struts-kotlin'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'struts:struts:1.2.9'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    providedCompile 'fish.payara.extras:payara-micro:4.1.1.161.1'
}

war {
    baseName = projectName
}

task payara(type: Exec, dependsOn: war) {
    def payaraJar = configurations
                        .providedCompile
                        .find {it.name =~ /payara-micro.*\.jar/}
                        .absolutePath
    def warFile = war.archivePath

    commandLine(
        'java',
        '-Xdebug',
        '-Xrunjdwp:transport=dt_socket,server=y,address=18000,suspend=n',
        '-jar', payaraJar, '--deploy', warFile
        )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

eclipse {
    project {
        name = projectName
        buildCommand 'org.jetbrains.kotlin.ui.kotlinBuilder'
        natures 'org.jetbrains.kotlin.core.kotlinNature'
        linkedResource(
            name: 'kotlin_bin',
            type: '2',
            locationUri: "org.jetbrains.kotlin.core.filesystem:/${name}/kotlin_bin"
        )
    }
    classpath.containers 'org.jetbrains.kotlin.core.KOTLIN_CONTAINER'
}
