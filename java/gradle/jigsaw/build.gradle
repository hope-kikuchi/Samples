apply plugin: 'application'

sourceCompatibility = 10
targetCompatibility = 10

compileJava.options.encoding = 'UTF-8'

ext.moduleName = "sample.jigsaw"

mainClassName = "${moduleName}/sample.jigsaw.Main"

jar.baseName = "jigsaw-sample"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.7'
}

//*
compileJava {
    doFirst {
        options.compilerArgs = [
            "--module-path", classpath.asPath
        ]
        classpath = files()
    }
}

run {
    doFirst {
        main = ""
        jvmArgs = [
            "--module-path", classpath.asPath
        ]
        args = ["--module", mainClassName]
        args += ["foo", "bar"]
        classpath = files()
    }
}


import java.util.regex.Matcher
startScripts {
    doFirst {
        classpath = files()
        defaultJvmOpts = [
            "--module-path", "APP_HOME_LIBS",
            "--module", mainClassName
        ]
    }
    
    doLast {
        File bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent
                            .replaceFirst("APP_HOME_LIBS", Matcher.quoteReplacement('$APP_HOME/lib'))
                            .replace('-classpath "\\"$CLASSPATH\\"" ' + mainClassName, '')

        File batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent
                            .replaceFirst("APP_HOME_LIBS", Matcher.quoteReplacement('%APP_HOME%/lib'))
                            .replace('-classpath "%CLASSPATH%" ' + mainClassName, '')
    }
}
//*/
