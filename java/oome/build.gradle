apply plugin: 'war'
apply plugin: 'eclipse'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

ext {
    projectName = 'OutOfMemoryErrorExercise'
}

jar {
    baseName = projectName
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    providedCompile 'fish.payara.extras:payara-micro:4.1.1.161.1'
}

eclipse {
    project.name = projectName
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task payara(type: Exec, dependsOn: war) {
    def payaraJar = configurations
                        .providedCompile
                        .find {it.name =~ /payara-micro.*\.jar/}
                        .absolutePath
    def warFile = war.archivePath

    commandLine(
        'java',
        '-verbose:gc',
        '-XX:+HeapDumpOnOutOfMemoryError',
        '-XX:HeapDumpPath=./hprof/',
        '-Xloggc:./gclog/gc.log',
        '-XX:+PrintGCDateStamps',
        '-XX:+PrintGCDetails',
        '-XX:+UseGCLogFileRotation',
        '-XX:NumberOfGCLogFiles=5',
        '-XX:GCLogFileSize=10m',
        '-Xms256m',
        '-Xmx512m',
        '-Xdebug',
        '-Xrunjdwp:transport=dt_socket,server=y,address=18000,suspend=n',
        '-jar', payaraJar, '--deploy', warFile
    )
}
